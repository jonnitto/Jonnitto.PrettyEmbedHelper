prototype(Jonnitto.PrettyEmbed:Presentation.Lightbox) < prototype(Neos.Fusion:Component) {
    @propTypes {
        player = ${PropTypes.oneOf(['YouTube', 'Vimeo', 'Video']).isRequired}
        content = ${PropTypes.string}
        aspectRatio = ${PropTypes.anyOf(PropTypes.string, PropTypes.float, PropTypes.integer)}
        playButton = ${PropTypes.boolean}
        pauseButton = ${PropTypes.boolean}
   }

    player = 'Video'
    playButton = ${this.player == 'Video'}

    @if.hasPlayer = ${this.player}

    renderer = afx`
        <template x-teleport="body">
            <div
                x-show="lightbox"
                "x-transition.opacity.duration.500ms"
                x-cloak
                aria-hidden="true"
                class="jonnitto-prettyembed-backdrop"
            ></div>
        </template>
        <template x-teleport="body">
            <div
                x-show="lightbox"
                x-cloak
                "x-on:keydown.escape.window.prevent.stop"="lightbox=false"
                "x-transition.duration.500ms"
                role="dialog"
                aria-modal="true"
                class="jonnitto-prettyembed-lightbox"
            >
                <div x-on:click.outside="lightbox=false" class="jonnitto-prettyembed-lightbox__inner">
                    <Jonnitto.PrettyEmbed:Presentation.Button.Close />
                    <div
                        x-ref={props.player == 'Vimeo' ? 'vimeo' : null}
                        style={props.aspectRatio ? '--aspect-ratio:' + props.aspectRatio :  null}
                        class.base="jonnitto-prettyembed-lightbox__content"
                        class.ratio={props.aspectRatio ? "jonnitto-prettyembed-ratio" : null}
                    >
                        <Jonnitto.PrettyEmbed:Presentation.YouTubeTarget
                            player={props.player}
                            controls={!props.pauseButton}
                        />
                        {props.player == 'Video' ? props.content : ''}
                        <Jonnitto.PrettyEmbed:Presentation.Button.Play
                            @if={props.playButton}
                            onClick="play"
                        />
                        <Jonnitto.PrettyEmbed:Presentation.Button.Pause  @if={props.pauseButton} />
                    </div>
                </div>
            </div>
        </template>
    `
}
