prototype(Jonnitto.PrettyEmbed:Presentation.Preview) < prototype(Neos.Fusion:Component) {
    @propTypes {
        content = ${PropTypes.string}
        show = ${PropTypes.string}
        label = ${PropTypes.string}
        src = ${PropTypes.string}
        src2x = ${PropTypes.string}
        srcset = ${PropTypes.string}
        width = ${PropTypes.integer}
        height = ${PropTypes.integer}
        loadImageStrategy = ${PropTypes.oneOf(['lazy', 'eager', null, false])}
    }

    @if.hasImageOrContent = ${this.content || this.src}

    srcset = null
    srcset.@process.convert = ${value || (this.src2x ? this.src + ' 1x,' + this.src2x + ' 2x' : null)}

    renderer = afx`
        <Neos.Fusion:Augmenter
            @if={props.content}
            x-show={props.show}
            content={props.content}
        />
        <img
            @if={!props.content}
            class="jonnitto-prettyembed-preview"
            x-show={props.show}
            alt={props.label}
            src={props.src}
            srcset={props.srcset}
            height={props.height}
            width={props.width}
            loading={props.loadImageStrategy}
        />
    `
}
